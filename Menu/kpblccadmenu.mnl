;; mnl

(vl-load-com)

(defun menu-loader-and-all-updates (/ fun_append-zero-to-value fun_get-file-systime fun_get-xml-files local_loader local_path msg net_loader_name server_loader server_paths
                                   )
                                   ;|
  *    Выполняет проверку необходимости обновления NET-сборки загрузчика
  *    При необходимости - загрузка того самого загрузчика (а он, в свою очередь, уже будет обновлять и загружать
  * все остальные модули - NEt / ARX / LSP / FAS / VLX ...
  *    Имя "загрузчика" и перечень каталогов в которых он может находиться, будет напрямую прописано в этом коде
  |;

  (defun fun_append-zero-to-value (value len)
                                  ;|
  *    Дополняет переданную строку нулем
  *    Параметры вызова:
    value  ; обрабатываемая строка
    len    ; результирующая длина строки
  *    Пример вызова:
  (fun_append-zero-to-value "123" 6) ; "000123")
  |;
    (while (< (strlen value) len)
      (setq value (strcat "0" value))
    ) ;_ end of while
    value
  ) ;_ end of defun

  (defun fun_get-file-systime (filename / date)
                              ;|
    *    Возвращает время файла
    *    Параметры вызова:
      filename  ; обрабатываемое имя файла
    |;
    (setq date (vl-file-systime filename)
          date (apply (function strcat)
                      (mapcar
                        (function
                          (lambda (x)
                            (fun_append-zero-to-value (itoa (nth x date)) 2)
                          ) ;_ end of lambda
                        ) ;_ end of function
                        '(0 1 3 4 5 6 7)
                      ) ;_ end of mapcar
               ) ;_ end of apply
    ) ;_ end of setq
  ) ;_ end of defun

  (defun fun_get-xml-files (folder)
                           ;|
    *    Получает все xml-файлы из указанного каталога с их датами
    *    Параметры вызова:
      folder ; каталог, внутри которого искать xml файл
    *    ВОзвращает список списков типа:
     '((<ИМЯ ФАЙЛА ЗАГЛАВНЫМИ> <Полный путь к файлу> <Дата файла>)
       (<ИМЯ ФАЙЛА ЗАГЛАВНЫМИ> <Полный путь к файлу> <Дата файла>)
       ...
       )
    |;
    (setq folder (vl-string-right-trim "\\" folder))
    (vl-sort (mapcar
               (function
                 (lambda (file)
                   (list (strcase file) (setq file (strcat folder file)) (fun_get-file-systime file))
                 ) ;_ end of LAMBDA
               ) ;_ end of function
               (vl-directory-files folder "*.xml")
             ) ;_ end of mapcar
             (function (lambda (a b)
                         (< (car a) (car b))
                       ) ;_ end of lambda
             ) ;_ end of function
    ) ;_ end of vl-sort
  ) ;_ end of defun

  (setq
          ; имя загрузчика
    net_loader_name "kpblccadloader.dll"
          ; перечень возможных серверных путей, где он будет болтаться
    server_paths    '("\\\\server\\apps\\loader"
                      "\\\\\\vmware-host\\Shared Folders\\Share\\KpblcCadSample\\loader"
                     )
          ; локальный путь расположения загрузчика
    local_path      (strcat (getenv "appdata") "\\KpblcCadSample\\Loader")
  ) ;_ end of setq

  (if (findfile (setq local_loader (strcat local_path "\\" net_loader_name)))
    (progn
      (if (setq server_loader
                 (car (vl-remove nil
                                 (mapcar
                                   (function
                                     (lambda (folder)
                                       (findfile (strcat folder "\\" net_loader_name))
                                     ) ;_ end of lambda
                                   ) ;_ end of function
                                   server_paths
                                 ) ;_ end of mapcar
                      ) ;_ end of vl-remove
                 ) ;_ end of car
          ) ;_ end of setq
        (progn
          ;; Серверный загрузчик найден, можно пробовать обновить локальную копию
          (setq local_loader_datetime
                 (fun_get-file-systime local_loader)
                server_loader_datetime
                 (fun_get-file-systime server_loader)
          ) ;_ end of setq
          (if (< local_loader_datetime server_loader)
            (progn
              (vl-catch-all-apply
                (function (lambda ()
                            (vl-file-delete local_loader)
                          ) ;_ end of lambda
                ) ;_ end of function
              ) ;_ end of vl-catch-all-apply
              (if (not (findfile local_loader))
                (vl-file-copy server_loader local_loader)
              ) ;_ end of if
            ) ;_ end of progn
          ) ;_ end of if

          ;; Получаем все xml-файлы из серверного каталога стартера
          (setq server_xml (fun_get-xml-files (VL-FILENAME-DIRECTORY server_loader))
          ;; Получаем все xml-файлы из локального каталога стартера
                local_xml (fun_get-xml-files (VL-FILENAME-DIRECTORY local_loader)))

          (if (or (not (equal (mapcar (function car) server_xml)
                              (mapcar (function car) local_xml)
                       ) ;_ end of equal
                  ) ;_ end of not
                  (apply (function or)
                         (mapcar (function (lambda (local_file server_file)
                                             (< (last local_file)
                                                (last server_file)
                                             ) ;_ end of <
                                           ) ;_ end of lambda
                                 ) ;_ end of function
                                 local_xml
                                 server_xml
                         ) ;_ end of mapcar
                  ) ;_ end of apply
              ) ;_ end of or
            (progn
              (foreach file (mapcar (function cadr) local_xml)
                (vl-file-delete file)
              ) ;_ end of foreach
              (foreach file (mapcar (function cadr) server_xml)
                (vl-file-copy
                  file
                  (strcat (vl-filename-directory local_loader)
                          "\\"
                          (vl-filename-base file)
                          (vl-filename-extension file)
                  ) ;_ end of strcat
                ) ;_ end of vl-file-copy
              ) ;_ end of foreach
            ) ;_ end of progn
          ) ;_ end of if
          
        ) ;_ end of progn
      ) ;_ end of if
      (if (findfile local_loader)
        ((lambda (/ sysvars)
           (setq sysvars (vl-remove nil
                                    (mapcar
                                      (function
                                        (lambda (x / temp)
                                          (if (setq temp (getvar (car x)))
                                            (progn
                                              (setvar (car x) (cdr x))
                                              (cons (car x) temp)
                                            ) ;_ end of progn
                                          ) ;_ end of if
                                        ) ;_ end of lambda
                                      ) ;_ end of function
                                      '(("cmdecho" . 0)
                                        ("menuecho" . 0)
                                        ("nomutt" . 1)
                                       )
                                    ) ;_ end of mapcar
                         ) ;_ end of vl-remove
           ) ;_ end of setq
           (vl-cmdf "_.netload" local_loader)
           (foreach item sysvars
             (setvar (car item) (cdr item))
           ) ;_ end of foreach
         ) ;_ end of lambda
        )
      ) ;_ end of if
    ) ;_ end of progn
    (progn
      (setq msg (strcat "Загрузчик не найден по пути " local_loader))
      (alert msg)
      (princ msg)
    ) ;_ end of progn
  ) ;_ end of if
  (princ)
) ;_ end of defun
